/**
 * build.gradle for erm-dataservice-client subproject
 * Created by jeremybrown on 26/10/2016
 **/

buildscript {
    ext {
        applicationName = 'erm-dataservice-client'
        applicationDescription = 'ERM Dataservice Client'
        basePackageName = property('groupId') + ".client"
    }
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
}

plugins {
    id 'org.detoeuf.swagger-codegen' version '1.6.3'
}

jar {
    baseName = "${applicationName}"
    version = "${applicationVersion}"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

publishing {
    publications {
        DataserviceClientPublication(MavenPublication) {
            from components.java
            groupId project.property('groupId')
            artifactId "${applicationName}"
            version "${applicationVersion}"
        }
    }
}

bintray {
    publications = ['DataserviceClientPublication']
    pkg {
        repo = 'erm-maven'
        name = "${applicationName}"
        userOrg = 'soen387-fall2016'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/soen387-fall2016/erm-dataservice'

        version {
            name = "${applicationVersion}"
            desc = "${applicationDescription}"
            released  = new Date()
        }
    }
}

swagger {
    inputSpec = 'http://54.218.174.224:8080/v2/api-docs'

    output = 'build/swagger'
    language = 'java'

    additionalProperties = [
            'invokerPackage'   : "${basePackageName}".toString(),
            'modelPackage'     : "${basePackageName}".toString() + ".model",
            'apiPackage'       : "${basePackageName}".toString() + ".api",
            'dateLibrary'      : 'java8'
    ]
    apis = ''
    models = ''
}

sourceSets {
    swagger {
        java {
            srcDir file("${project.buildDir.path}/swagger/src/main/java")
        }
    }
}

ext {
    jackson_version = '2.4.2'
    jersey_version = '1.18'
    jodatime_version = '2.3'
    junit_version = '4.8.1'
}

dependencies {
    swaggerCompile 'org.springframework.boot:spring-boot-starter-web'

    compile "com.sun.jersey:jersey-client:$jersey_version"
    compile "com.sun.jersey.contribs:jersey-multipart:$jersey_version"
    compile "com.fasterxml.jackson.core:jackson-core:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-annotations:$jackson_version"
    compile "com.fasterxml.jackson.core:jackson-databind:$jackson_version"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.1.5"
    compile "joda-time:joda-time:$jodatime_version"

    testCompile "junit:junit:$junit_version"
}
